#include <NewPing.h>
#include <Sonar.h>

//LED
#define LEDPIN 11
#define LED_FREQUENCY_HZ 4500
#define KILLALLTIME_MS 3000

//Servo
#define AZMPIN 3
#define BASEPULSE_US 20000 // 50 Hz
#define CLOCKWISE_US 1000
#define CNTRCLOCKWISE_US 2000
#define SMALLCORRECTION (1.273)
#define US_PER_DEGREE 3.3E3

signed int currentAngle = 0;

#define MAXANGLE 180 // Used in setting current position limits in naming locations
#define MINANGLE 0
#define NUM_OF_BINS 5
#define ANGLESLICE_DEG ((MAXANGLE-MINANGLE)/NUM_OF_BINS)

//Sonar
#define SONARPIN 6
Sonar SONAR1(SONARPIN);

//Calibration
unsigned calibrationBin = 0; //The current bin that calibration is setting
bool countingDown = false;

unsigned long long killAllTimeMark = 0; //Start time that killAll was initiated
bool incFlag = true;
unsigned int locationCounter = 0; //Current bin to check for hits in
/*
   We need to get the switch player values
   One full set up circle
   Rotate
   Sample
   If there is a hit, (already at angle)
   Sound Alarm, Fire 5 seconds
   Check Again,
   Either Continue firing or start search again
*/
enum controlState_t {INIT, CALIBRATION, MOTION, SAMPLE, SOUNDALERT, KILLALL, REST};
controlState_t controlState = INIT;

void setup() {
  pinMode(AZMPIN, OUTPUT);
  pinMode(LEDPIN, OUTPUT);
  Serial.begin(9600);
  delay(1000); //Time for Serial to init
  SONAR1.reset();
}

void loop() {
  controlSMtick();
}

void debugStatePrint()
{
  static controlState_t lastState = INIT;

  if (controlState != lastState)
  {
    lastState = controlState;
    Serial.println("CurrentState Is:");
    switch (controlState)
    {
      case INIT:
        Serial.println("INIT");
        break;

      case CALIBRATION:
        Serial.println("CALIBRATION");
        break;

      case MOTION:
        Serial.println("MOTION");
        break;

      case SAMPLE:
        Serial.println("SAMPLE");
        break;

      case SOUNDALERT:
        Serial.println("SOUNDALERT");
        break;

      case KILLALL:
        Serial.println("KILLALL");
        break;

      case REST:
        Serial.println("REST");
        break;

      default:
        Serial.println("default");
    }
  }
}

//////////////////////////
/////// LED control /////
////////////////////////

void ledOn()
{
  tone(LEDPIN, LED_FREQUENCY_HZ);
}

// Disable LED Strobe
void ledOff()
{
  noTone(LEDPIN);
}

void rotateCW(unsigned long duration)
{
  unsigned long startTime = micros();


  while ( (micros() - startTime) < duration  )
  {
    unsigned long pwmTime = micros();
    // OFF -> ON
    while ((micros() - pwmTime) < CLOCKWISE_US )
    {
      digitalWrite(AZMPIN, HIGH);
    }

    pwmTime = micros();

    while ( (micros() - pwmTime) < (BASEPULSE_US - CLOCKWISE_US) )
    {
      digitalWrite(AZMPIN, LOW);
    }

  }

}

void rotateCCW(unsigned long duration)
{
  unsigned long startTime = micros();


  while ( (micros() - startTime) < duration  )
  {
    unsigned long pwmTime = micros();
    // OFF -> ON
    while ((micros() - pwmTime) < CNTRCLOCKWISE_US )
    {
      digitalWrite(AZMPIN, HIGH);
    }

    pwmTime = micros();

    while ( (micros() - pwmTime) < (BASEPULSE_US - CLOCKWISE_US) )
    {
      digitalWrite(AZMPIN, LOW);
    }

  }
}

////////////////////
// ANGLE CODE /////
///////////////////

void goToDegree(signed int azm)
{
  //Serial.println("Target Angle");
  //Serial.println(azm);

  // Get the difference
  signed int difference = azm - getCurrentAngle();

  //Serial.println("Difference");
  //Serial.println(difference);

  // If difference is positive,


  if (difference > 0)
  {
    // Rotate  CCW
    if (difference < 180)
    {


      rotateDegCCW(difference);
    } else
    {

      rotateDegCW(360 - difference);
    }
  }

  if (difference < 0)
  {
    // Rotate  CCW
    if (abs(difference) < 180)
    {

      rotateDegCW(abs(difference));
    } else
    {


      rotateDegCCW(360 - abs(difference));
    }
  }
}

// Rotates the given number of degrees
void rotateDegCW(unsigned int azmDeg)
{
  double correction = 1.0;
  if (azmDeg < 20)
  {
    correction = SMALLCORRECTION;
  }

  unsigned long runTime = (azmDeg * US_PER_DEGREE * correction);
  signed int negDelta = -1 * azmDeg;
  updateAngle(negDelta);
  rotateCW(runTime);
}

// Rotates the given number of degrees
void rotateDegCCW(unsigned int azmDeg)
{
  double correction = 1.0;
  if (azmDeg < 20)
  {
    correction = SMALLCORRECTION;
  }

  unsigned long runTime = (azmDeg * US_PER_DEGREE * correction);
  updateAngle(azmDeg);
  rotateCCW(runTime);
}

// Updates the current location of the servo.
// Handles the rollover degrees
// positive deltaDeg is CW
// negative deltaDeg is CCW
void updateAngle(signed int deltaDeg)
{
  currentAngle = currentAngle + deltaDeg;

  // Handle Rollover
  if (currentAngle > MAXANGLE)
  {
    currentAngle = (currentAngle - MAXANGLE) + MINANGLE;
  }
  else if (currentAngle < MINANGLE)
  {
    currentAngle = MAXANGLE - abs(MINANGLE - currentAngle);
  }
  //Serial.println("Current Angle Is:");
  //Serial.println(currentAngle);
}

// Returns the current angular position
signed int getCurrentAngle()
{
  return currentAngle;
}


///////////////////////////////
////// State Machine /////////
/////////////////////////////

void controlSMtick()
{
  debugStatePrint();

  switch (controlState)
  {
    case INIT:
      {
        locationCounter = 0;
        controlState = CALIBRATION;
        incFlag = true;
        calibrationBin = 0;
        countingDown = false;
      }
      break;

    case CALIBRATION:
      {
        if (calibrationBin == 0 && countingDown) {
          controlState = MOTION;
        }
        else {
          controlState = CALIBRATION;
        }

        signed int angle = (ANGLESLICE_DEG * calibrationBin);
        goToDegree(angle);
        SONAR1.setNormalRange(calibrationBin);

        //Calculate next calibration bin position
        if (calibrationBin == NUM_OF_BINS) {
          countingDown = true;
        }

        if (countingDown) {
          --calibrationBin;
        }
        else {
          ++calibrationBin;
        }
        break;
      }

    case MOTION:
      {
        signed int angle = (ANGLESLICE_DEG * locationCounter);
        goToDegree(angle);
        controlState = SAMPLE;

        if (incFlag)
        {
          if ((locationCounter == NUM_OF_BINS))
          {
            locationCounter--;
            incFlag = false;
          }
          else
          {
            locationCounter++;
          }
        }
        else
        {
          if ((locationCounter == 0))
          {
            locationCounter++;
            incFlag = true;
          } 
          else
          {
            locationCounter--;
          }
        }
      }
      break;

    case SAMPLE:
      if (SONAR1.hitDetected(locationCounter)) {
        controlState = SOUNDALERT;
      }
      else {
        controlState = MOTION;
      }
      break;
    case SOUNDALERT:
      {
        controlState = KILLALL;
        killAllTimeMark = millis();
      }
      break;
    case KILLALL:
      {
        if ((millis() - killAllTimeMark) >= (KILLALLTIME_MS))
        {
          controlState = MOTION;
          ledOff();
        }
        else {
          ledOn();
          controlState = KILLALL;
        }
      }
      break;

    case REST:
      {
        controlState = MOTION;
      }
      break;
    default:
      {
        controlState = INIT;
      }
      break;
  }
}
